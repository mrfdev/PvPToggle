plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.0.0"
}

group = 'dev.the456gamer'
version = '1.7.1'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        // papi
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        // nametagedit
        url = "https://ci.nametagedit.com/plugin/repository/everything"
    }
    maven {
        // weird bstats group
        // acf
        // both for sub depends
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'com.nametagedit:nametagedit:4.5.16'

}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.19.4")
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
